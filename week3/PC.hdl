// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
        Mux16(a=incRegOut, b=in, sel=load, out=selectedOut);
        Mux16(a=selectedOut, b[0..15]=false, sel=reset, out=regIn);
        Add16(a=regOut, b[0]=inc, b[1..15]=false, out=incRegOut); // b[0..14]=false, b[15]=inc
        // Inc16(in=regOut, out=incRegOut);
        Register(in=regIn, load=true, out=out, out=regOut);
}
